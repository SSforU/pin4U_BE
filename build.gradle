plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.flywaydb.flyway" version "11.7.2"
}

flyway {
    url = "jdbc:postgresql://localhost:5432/pin4u_be"
    user = "pin4u"
    password = "pin4u"
    locations = ["classpath:db/migration"]
}

group = 'io.github.ssforu'
version = '0.0.1-SNAPSHOT'
java { toolchain { languageVersion = JavaLanguageVersion.of(17) } }

configurations {
    compileOnly { extendsFrom annotationProcessor }
}

dependencies {
    // Web & UI
    implementation 'org.springframework.boot:spring-boot-starter-web:3.5.4'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.5.4'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:3.5.4'
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.5.4' // WebClient 사용 시 필요

    // Data (JPA, Redis, PostgreSQL)
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.5.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis:3.5.4' // [추가] Redis 필수
    runtimeOnly 'org.postgresql:postgresql:42.7.7'

    // Migration
    implementation 'org.flywaydb:flyway-core:11.7.2'
    implementation 'org.flywaydb:flyway-database-postgresql:11.7.2'

    // Monitoring & Docs (Actuator, Prometheus, Swagger)
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.5.4'
    implementation 'io.micrometer:micrometer-registry-prometheus' // [중요] 런타임이 아니라 구현 의존성으로 추가
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5")

    // Cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

    // 메모리 캐시(Caffeine)
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // Utils
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:3.5.4'
    developmentOnly 'org.springframework.boot:spring-boot-devtools:3.5.4'

    // Test
    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.5.4'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers:3.5.4'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.2'
    testImplementation 'org.testcontainers:postgresql:1.20.2'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    onlyIf { project.hasProperty("withTests") }
    systemProperty "spring.testcontainers.enabled", "false"
}

springBoot {
    mainClass = "io.github.ssforu.pin4u.Pin4uApplication"
    buildInfo() // Actuator Info 엔드포인트 활성화
}

tasks.named("bootJar") {
    dependsOn tasks.named("bootBuildInfo")
}